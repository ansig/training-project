plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.google.cloud.tools.jib' version '2.6.0'
	id 'java'
	id "com.gorylenko.gradle-git-properties" version "2.2.4"
}

group = 'se.omegapoint.training'
version = "${baseVersion}-${buildVersion}"
sourceCompatibility = '11'
targetCompatibility = '11'

processResources {
	expand(project.properties)
}

springBoot {
	buildInfo()
}

gitProperties {
	keys = ['git.branch','git.commit.id']
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:2.4.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:2.4.0'
	testImplementation('org.springframework.boot:spring-boot-starter-test:2.4.0') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

jib {
	to {
		image = "harbor.harbor/op-cicd-training/${project.name}"
		tags = [project.version]
		auth {
			username = System.getenv('HARBOR_USER')
			password = System.getenv('HARBOR_PASSWORD')
		}
	}
	container {
		ports = ['8080']
		jvmFlags = []
	}
	allowInsecureRegistries = true
}
